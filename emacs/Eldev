; -*- mode: emacs-lisp; lexical-binding: t; no-byte-compile: t -*-

;; Inject private configurations
(let ((private-dir (expand-file-name "Dropbox/emacs/" (getenv "HOME"))))
  (when (file-exists-p private-dir)
    (let ((files (seq-filter
                  (lambda (file)
                    (and (string-suffix-p ".el" file)
                         (not (string-match ".*#.*" file))
                         (not (string-prefix-p "flycheck_" file))))
                  (directory-files private-dir nil "^\\([^.]\\|\\.[^.]\\|\\.\\..\\)"))))
      (mapc
       (lambda (file)
         (shell-command-to-string
          (format
           "safe_link %s%s %s/lisp/%s" private-dir file eldev-project-dir file)))
       files))))

;; Be explicit about main file.
(setf eldev-project-main-file "init.el"
      eldev-main-fileset '(:and ("init.el"
                                 "early-init.el"
                                 "lisp/*.el")
                                (:not ("**/flycheck_*.el"))))



;; Emacs doesn't allow to add directory containing init.el to load
;; path, so we have to put other Emacs Lisp files in directory. Help
;; Eldev commands to locate them.
(eldev-add-loading-roots 'build "lisp")
(eldev-add-loading-roots 'bootstrap "lisp")



;; There are dependencies for testing and linting phases, they should
;; be installed by Eldev from MELPA and GNU ELPA (latter is enabled by
;; default).
(eldev-use-package-archive 'melpa)


;; Bootstrap flow. Basically set `elpa-bootstrap-p' to `t' and load
;; init.el, which forces `init-elpa' to install all dependencies.

(defun elpa-bootstrap ()
  "Bootstrap personal configurations."
  (setq-default
   elpa-bootstrap-p t
   load-prefer-newer t)
  (eldev--inject-loading-roots 'bootstrap)
  (require 'config-path)
  (load (expand-file-name "init.el" path-emacs-dir))
  (unless (file-exists-p (expand-file-name "versions/default.el"
                                           path-emacs-dir))
    (straight-freeze-versions)))

;; We want to run this before any build command. This is also needed
;; for `flyspell-eldev` to be aware of packages installed via
;; straight.el.
(add-hook 'eldev-build-system-hook #'elpa-bootstrap)


;; Upgrade flow. Hooks into Eldev upgrade flow.

(defun elpa-upgrade ()
  "Bootstrap personal configurations."
  (elpa-bootstrap)
  (straight-fetch-all)
  (straight-merge-all)
  (straight-thaw-versions)
  (delete-file (concat path-packages-dir "straight/build-cache.el"))
  (delete-directory (concat path-packages-dir "straight/build") 'recursive)
  (straight-check-all)
  (straight-freeze-versions))

(add-hook 'eldev-upgrade-hook #'elpa-upgrade)



;; We want to generate autoloads file. This line simply loads few
;; helpers.
(eldev-use-plugin 'autoloads)

;; Eldev doesn't traverse extra loading roots, so we have to modify
;; autoloads plugin a little bit. Basically, this modification
;; achieves specific goal - generate autoloads from files located in
;; Lisp directory.
(eldev-defbuilder eldev-builder-autoloads (sources target)
  :type           many-to-one
  :short-name     "AUTOLOADS"
  :message        target
  :source-files   (:and "lisp/*.el" (:not ("lisp/*autoloads.el")))
  :targets        (lambda (_sources) "lisp/init-autoloads.el")
  :define-cleaner (eldev-cleaner-autoloads
                   "Delete the generated package autoloads files."
                   :default t)
  :collect        (":autoloads")
  ;; To make sure that `update-directory-autoloads' doesn't grab files it shouldn't,
  ;; override `directory-files' temporarily.
  (eldev-advised (#'directory-files
                  :around
                  (lambda (original directory &rest arguments)
                    (let ((files (apply original directory arguments)))
                      (if (file-equal-p directory eldev-project-dir)
                          (let (filtered)
                            (dolist (file files)
                              (when (eldev-any-p (file-equal-p file it) sources)
                                (push file filtered)))
                            (nreverse filtered))
                        files))))
    (let ((inhibit-message   t)
          (make-backup-files nil)
          (pkg-dir (expand-file-name "lisp/" eldev-project-dir)))
      (package-generate-autoloads (package-desc-name (eldev-package-descriptor)) pkg-dir)
      ;; Always load the generated file.  Maybe there are cases when we don't need that,
      ;; but most of the time we do.
      (eldev--load-autoloads-file (expand-file-name target eldev-project-dir)))))



;; Always load autoloads file.
(add-hook
 'eldev-build-system-hook
 (lambda ()
   (eldev--load-autoloads-file
    (expand-file-name "lisp/init-autoloads.el" eldev-project-dir))))


;; Linting configurations.

;; Tell checkdoc not to demand two spaces after a period.
(setq sentence-end-double-space nil)

(defun eldev-lint-find-files-absolute (f &rest args)
  "Call F with ARGS and ensure that result is absolute paths."
  (seq-map (lambda (p)
             (expand-file-name p eldev-project-dir))
           (seq-filter (lambda (p)
                         (not (string-suffix-p "autoloads.el" p)))
                       (apply f args))))

(advice-add 'eldev-lint-find-files :around #'eldev-lint-find-files-absolute)

;; In general, `package-lint' is useful. But package prefix naming
;; policy is not useful for personal configurations. So we chop
;; lib/init part from the package name.
;;
;; And `eval-after-load'. In general it's not a good idea to use it in
;; packages, but these are configurations.
(with-eval-after-load 'package-lint
  (defun package-lint--package-prefix-cleanup (prefix)
    "Cleanup PREFIX."
    (replace-regexp-in-string
     "\\(private\\)-?" ""
     (replace-regexp-in-string
      "\\(init\\|lib\\|config\\|compat\\)-?" ""
      prefix)))

  (defun package-lint--get-package-prefix-wrapper (f &rest args)
    "Call F with ARGS and cleanup it's result."
    (let ((r (apply f args)))
      (package-lint--package-prefix-cleanup r)))
  (advice-add 'package-lint--get-package-prefix :around #'package-lint--get-package-prefix-wrapper)

  (defun package-lint--check-defs-prefix-wrapper (f prefix definitions)
    "Call F with cleared PREFIX and DEFINITIONS."
    (funcall f (package-lint--package-prefix-cleanup prefix) definitions))
  (advice-add 'package-lint--check-defs-prefix :around #'package-lint--check-defs-prefix-wrapper)

  (defun package-lint--check-defalias-wrapper (f prefix def)
    "Call F with cleared PREFIX and DEF."
    (funcall f (package-lint--package-prefix-cleanup prefix) def))
  (advice-add 'package-lint--check-defalias :around #'package-lint--check-defalias-wrapper)

  (defun package-lint--check-eval-after-load ()
    "Do nothing.")
  (defun package-lint--check-autoloads-on-private-functions (&rest _)
    "Do nothing.")

  (defun package-lint--check-emacs-version (_)
    "Do nothing"))

;; Teach linter how to properly indent emacsql vectors.
(eldev-add-extra-dependencies 'lint 'emacsql)
(add-hook 'eldev-lint-hook
          (lambda ()
            (eldev-load-project-dependencies 'lint nil t)
            (require 'emacsql)
            (call-interactively #'emacsql-fix-vector-indentation)))



;; compatibility for `nano-layout'

(defun scroll-bar-mode-wrapper (f &optional arg)
  (when (display-graphic-p)
    (funcall f arg)))
(advice-add 'scroll-bar-mode :around #'scroll-bar-mode-wrapper)


